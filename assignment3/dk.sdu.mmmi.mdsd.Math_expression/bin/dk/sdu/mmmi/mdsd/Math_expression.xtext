grammar dk.sdu.mmmi.mdsd.Math_expression with org.eclipse.xtext.common.Terminals

generate math_expression "http://www.sdu.dk/mmmi/mdsd/Math_expression"

MathExp:
	variables += VarBinding*
;

VarBinding:
	'var' name=ID '=' expression=Exp
;

Exp returns Expression:
	Factor (( {Plus.left=current} '+' | {Minus.left=current}  '-' ) right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current}  '/'  ) right=Primary)*
;

Primary returns Expression:
	{MathNumber} value=INT  | '(' Exp ')'  |  VariableUse | LetBinding
;

LetBinding:
	'let' name=ID '=' binding=Exp 'in' body=Exp 'end'
;

Binding:
	VarBinding | LetBinding
;

VariableUse:
	ref = [Binding]
;
