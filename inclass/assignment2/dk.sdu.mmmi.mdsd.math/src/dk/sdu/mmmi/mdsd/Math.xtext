grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

MathExp:
	//'program' name=ID
	varExp+=VariableExp*
;

VariableExp:
	'var' name=ID '=' exp=Exp
;

Exp:
	PlusMinus
;

PlusMinus returns Exp:
	Term (
		{Plus.left=current} '+' right=Term |
		{Minus.left=current} '-' right=Term
	)*
;

Term returns Exp:
	Primary (
		{Mult.left=current} '*' right=Primary |
		{Div.left=current} '/' right=Primary
	)*
;

Primary returns Exp:
	{Number} value=INT | VariableUse | '(' Exp ')' | LetBinding
;

LetBinding:
	'let' name=ID '=' binding=Exp 'in' body=Exp 'end'
;

VariableUse:
	ref = [VariableBinding]
;

VariableBinding:
	VariableExp | LetBinding
;