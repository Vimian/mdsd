grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

MathExp:
	variables += Variable*
;

Variable:
	'var' name=ID '=' exp=Exp
;

Exp:
	Factor ((
		{Plus.left=current} '+' right=Factor |
		{Minus.left=current} '-' right=Factor
	))*
;

Factor returns Exp:
	Primary ((
		{Multi.left=current} '*' right=Primary |
		{Divi.left=current} '/' right=Primary
	))*
;

Primary:
	MathNumber | Parenthesis | LetVariable | VariableUse
;

Parenthesis:
	'(' exp=Exp ')'
;

MathNumber:
	value=INT
;

LetVariable:
	'let' name=ID '=' value=Exp 'in' exp=Exp 'end'
;

VariableType:
	Variable | LetVariable
;

VariableUse:
	ref = [VariableType]
;